# shortcut to this dotfiles path is $DOTS
export DOTS=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
# export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localenv ]]
then
  source ~/.localenv
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTS/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Make Vi mode transitions faster (KEYTIMEOUT is in hundredths of a second)
export KEYTIMEOUT=1

# Enable oh-my-zsh if it exists
if [[ -d $HOME/.oh-my-zsh/ ]]
then
  export ZSH=$HOME/.oh-my-zsh

  # Enable spaceship theme
  ZSH_THEME="spaceship"
  # hide vi mode indicator until pull request that allows empty string gets merged
  SPACESHIP_VI_MODE_SHOW=false
  # SPACESHIP_VI_MODE_INSERT=""

  # Enable plugins
  plugins=(zsh-autosuggestions vi-mode)

  source $ZSH/oh-my-zsh.sh
fi
